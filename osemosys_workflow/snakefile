SCENARIOS = ['utopia1', 'utopia2']
rule all:
    input:
        expand("results/{scen}/results_csv/UseByTechnology.csv", scen=SCENARIOS) #for testing, to be changed during development

rule convert_dp:
    input:
        dp_path = "data/{scen}/datapackage.json"
    output:
        df_path = "data/{scen}/{scen}.txt"
    shell:
        "otoole convert datapackage datafile {input.dp_path} {output.df_path}"

rule build_lp:
    input:
        df_path = "data/{scen}/{scen}.txt"
    params:
        model_path = "model/osemosys.txt"
    output:
        lp_path = "data/{scen}/{scen}.lp"
    shell:
        "glpsol -m {params.model_path} -d {input.df_path} --wlp {output.lp_path} --check"

rule run_model:
    input:
        lp_path = "data/{scen}/{scen}.lp"
    params:
        ilp_path = "results/{scen}/{scen}.ilp"
    output:
        sol_path = "results/{scen}/{scen}.sol"
    shell:
        "gurobi_cl ResultFile={params.ilp_path} ResultFile={output.sol_path} {input.lp_path}"

rule convert_sol:
    input:
        sol_path = "results/{scen}/{scen}.sol",
        dp_path = "data/{scen}/datapackage.json"
    params:
        res_folder = "results/{scen}/results_csv"
    output:
        res_path = "results/{scen}/results_csv/UseByTechnology.csv"
    shell:
        "otoole results gurobi csv {input.sol_path} {params.res_folder} --input_datapackage {input.dp_path}"